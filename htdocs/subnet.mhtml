<%doc>
    Show a list of all addresses within a subnet.

</%doc>

<%args>
$network => undef
</%args>

<%init>
</%init>

<%perl>

    my %used;
    my %names;
    my %status;
    my %containers;

    my @children;  
    if (scalar( @children = $ipm->getchildren($network->id) )){
        foreach my $o ( @children ) {
            $used{ $o->address } = 1;
            my $name;
        	if ( my $rraddr = ($o->arecords)[0] ){
        	    $name = $ui->getobjlabel( $rraddr->rr, "," , returnAsVar=>1 );
        	}
            if( $o->status->name eq "Container" || $o->status->name eq "Reserved" ) {
                if( $o->description ) {
                    $names{ $o->address } = $o->description;
                } else {
                    if( $o->status->name eq "Container" ) {
                        $names{ $o->address } = "Container";
                    } else {
                        $names{ $o->address } = "Reserved Address";
                    }
                }
                $containers{ $o->address } = $o->id;
            } else {
                $names{ $o->address } = $name;
            }
            $status{ $o->address } = $o->status->name;
        }
    }    

    my $net = NetAddr::IP->new($network->address . "/" . $network->prefix);
    my $mask = $net->masklen;

    my @headers = ( );
    my @rows;
    my $cols;

    $net = $net->network();  # start printing from the first address, in case $net was not at the start of the subnet

    if( $mask > 29 ) {
        # if there are 8 or less addresses in the subnet, show only that many columns
        $cols = $net->num()+1;
    } elsif( $mask > 23 ) {
        # if there are 16-512 addresses, show them in 8 columns, and show the entire IPv4 address
        $cols = 8;
    } else {
        # if there are more than 512 addresses, show 16 columns, and don't show the insignificant octets
        $cols = 16;
    }

    for( my $i=0; $i<(($net->num()+1)/$cols); $i++ ) {
        my @row = ();

        for( my $j=0; $j<$cols; $j++ ) {
            my $ipaddr = $net->addr;

            # this code hides the insignificant (unchanging) octets from the ip address based on the subnet
            if( $mask <= 7 ) {
                # no insignificant octets
                $ipaddr = $ipaddr;
            } elsif( $mask <= 15 ) {
                # first octet is insignificant
                $ipaddr = substr($ipaddr, index($ipaddr,".")+1);
            } elsif( $mask <= 23 ) {
                # second octet is insignificant
                $ipaddr = substr($ipaddr, index($ipaddr,".",index($ipaddr,".")+1)+1);
            } else {
                # mask is 24 or bigger, show the entire ip address
                $ipaddr = $ipaddr;
            }


            if( $net == $net->network || $net == $net->broadcast ) {
                # don't allow selection of network or broadcast addresses
                if( $net == $net->network ) {
                    $ipaddr = '<div class="ipaddr_disabled" title="Network">'.$ipaddr.'</div>';
                } else {
                    $ipaddr = '<div class="ipaddr_disabled" title="Broadcast">'.$ipaddr.'</div>';
                }
            } elsif( defined $used{ $net->addr } ) {
                my $divclass;
                my $divtitle;

                if( $status{$net->addr} eq "Static" ) {
                    # show used addresses in red, with no link tag, show dns name on mouseover
                    $divclass = "ipaddr_static";
                    $divtitle = $names{ $net->addr } if exists $names{ $net->addr };

                } elsif( $status{$net->addr} eq "Dynamic" ) {
                    # show dynamic addresses in orange, don't link
                    $divclass = "ipaddr_dynamic";
                    $divtitle = "Dynamic address";

                } elsif( $status{$net->addr} eq "Reserved" ) {
                    # show reserved addresses in grey, don't link
                    $divclass = "ipaddr_reserved";
                    $divtitle = "Reserved address";

                } elsif( $status{$net->addr} eq "Container" ) {
                    # link to detail page for containers
                    $divclass = "ipaddr_container";
                    $divtitle = $names{ $net->addr } if exists $names{ $net->addr };
                    $ipaddr = '<a href="ip-ws.html?id='.$containers{ $net->addr }.'">'.$ipaddr.'</a>';
                }

                $ipaddr = '<div class="'.$divclass.'" title="'.$divtitle.'" width="100%">'.$ipaddr.'</div>';
            } else {
                # link this address to the "Add" section in ip-ws.html
                $ipaddr = '<div class="ipaddr_available"><a href="ip-ws.html?allocate='.$net->addr().'%2F32&_action=ALLOCATE">'.$ipaddr.'</a></div>';
            }

            push( @row, $ipaddr );
            $net++;                      # increment the IP address
        }

        push( @rows, \@row );
    }

</%perl>

<div style="margin-top:8px;margin-left:5px;">
    <span class="ipaddr_reserved">Reserved</span>
    <span class="ipaddr_static">Static</span>
    <span class="ipaddr_dynamic">Dynamic</span>
    <span class="ipaddr_available">Available</span>
</div>

    <hr>

<& data_table.mhtml, field_headers=>\@headers, data=>\@rows, subclass=>"ipb" &>
