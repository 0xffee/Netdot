<%doc>
    Show a list of IP blocks with relevant info.
    The hybrid character of IPblock objects (blocks and individual addresses) makes it necessary to
    display different info in each case.
    
    We expect an arrayref of Ipblock objects
    The array is supposed to be sorted.
    This is because objects are stored in decimal in the DB
    so it makes more sense to have the DB order them at 
    retrieval time.  After that, objects are "inflated" to
    a string, which is harder to sort.

    
</%doc>

<%args>
$objects => undef
$type    => undef
$edit    => 0
$sid     => undef
$osid    => undef
$table   => undef
</%args>

<%init>
my $session;
my $osession;
my %objindex;
my %orderbrief;
my @orderbrief;
my $i;



$i=0;
$session = $ui->mk_session();
map { $objindex{$i++} = $_ } @{ $objects };
$table = $objects->[0]->short_class unless $table;
%orderbrief  = $table->meta_data->get_column_order_brief();
@orderbrief  = sort { $orderbrief{$a} <=> $orderbrief{$b} } keys %orderbrief;

my (@headers, @rows);
if ( $type ){
    unless ( $type =~ /^address|block$/ ){
	$m->comp('error.mhtml', error => "Unknown type: $type");
    }
}else{
    if ( ($objects->[0])->is_address ){ # We're dealing with addresses
	$type = "address";
    }else{
	$type = "block";
    }
}
</%init>

<%perl>
if ( $type eq "address" ){
    if ( $edit ){
	push @headers, '[del]';
    }
    push @headers, ( 'Address', 'Name', 'Status', 'Used by', 'Description', 'Last Seen' );

    foreach my $o ( @{ $objects } ) {
	my @row = ();
	if ( $edit ){	 
	    push @row, '<input type="checkbox" name="' . "Ipblock__" . $o->id . "__delete" . '" >';
	}
	push( @row, '<a href="ip.html?id=' . $o->id . '">' . $o->address . '</a>' );

	if ( my $rraddr = ($o->arecords)[0] ){
	    push @row, $ui->form_field(object=>$rraddr, column=>"rr", edit=>$edit, returnValOnly=>1, linkPage=>1);
	} else {
	    push( @row, "");
	}

	$ui->add_to_fields(o=>$o, edit=>$edit, fields=>['status', 'used_by', 'description'], 
			   cell_data=>\@row, linkpages=>['', '', '']);

	$ui->add_to_fields(o=>$o, edit=>0, fields=>['last_seen'], cell_data=>\@row, );

	push( @rows, \@row );
    }
} elsif( $type eq "block" ) {
    @headers = ( 'Address/Prefix', 'Status', 'Utilization', 'Owner', 'Used by', 'Description' );

    foreach my $o ( @{ $objects } ) {
	my @row = ();
	push( @row, '<a href="ip.html?id=' . $o->id . '">' . $o->get_label . '</a>' );

	push @row, $ui->form_field(object=>$o, column=>"status", edit=>$edit, returnValOnly=>1);

	if ( $o->status->name eq "Subnet") {
	    push( @row, $ui->percent_bar(percent => (($o->num_children)/($o->num_addr)*100) ) );
	} elsif ($o->status->name eq "Container") {
	    
	    my $total = new Math::BigInt($o->num_addr);
	    my $used1 = new Math::BigInt($o->address_usage);
	    my $used2 = new Math::BigInt($o->subnet_usage);
	    my $percent1 = ($used1*100/$total);
	    my $percent2 = ($used2*100/$total);
	    
	    push( @row, $ui->percent_bar2( title1=>"Address Usage: ", title2=>"Subnet Usage: ", percent1=>$percent1, percent2=>$percent2 ) );
	} else {
	    push( @row, "" );
	}
	$ui->add_to_fields(o=>$o, edit=>$edit, fields=>['owner', 'used_by', 'description'],
			   cell_data=>\@row, linkpages=>['view.html', 'view.html', '']);

	push( @rows, \@row );
     }
}

$session->{object}      = $objects;
$session->{objindex}    = \%objindex;
$session->{table}       = $table;
$session->{orderbrief}  = \@orderbrief;
$session->{total}       = scalar @$objects;
</%perl>
<& /generic/data_table.mhtml, field_headers=>\@headers, data=>\@rows &>

<div class="containerheadright">
    <a href="#" onClick="opentextwindow('','csv','sid=<% $session->{_session_id} %>')">[csv]</a>
</div>

